plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.devtools.ksp'
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-parcelize'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.9.10'
    id 'com.mikepenz.aboutlibraries.plugin'
}

def screenshotMode = project.properties['SCREENSHOT_MODE'] ?: false

android {
    compileSdk 34

    defaultConfig {
        applicationId "io.github.drumber.kitsune"
        minSdk 26
        targetSdk 34
        versionCode 26
        versionName "1.10.4"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "boolean", "SCREENSHOT_MODE_ENABLED", screenshotMode
    }

    ksp {
        arg("room.schemaLocation", "$projectDir/schemas".toString())
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }

        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
        freeCompilerArgs += ["-opt-in=kotlin.RequiresOptIn"]
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    packagingOptions {
        exclude 'META-INF/*.kotlin_module'
    }
    namespace 'io.github.drumber.kitsune'
}

aboutLibraries {
    // Remove the "generated" timestamp to allow for reproducible builds
    excludeFields = ["generated"]
}

dependencies {
    // Android core and support libs
    implementation 'androidx.core:core-ktx:1.13.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.core:core-splashscreen:1.0.1'
    implementation "androidx.cardview:cardview:1.0.0"

    // Navigation
    def nav_version = "2.7.7"
    def fragment_version = "1.6.2"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.fragment:fragment-ktx:$fragment_version"

    // Preference
    implementation 'androidx.preference:preference-ktx:1.2.1'

    // Lifecycle
    def lifecycle_version = "2.7.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    // Material
    implementation 'com.google.android.material:material:1.11.0'

    // Kotlin coroutines
    def coroutines_version = "1.7.3"
    implementation('org.jetbrains.kotlinx:kotlinx-coroutines-core') {
        version {
            strictly "$coroutines_version"
        }
    }
    implementation('org.jetbrains.kotlinx:kotlinx-coroutines-android') {
        version {
            strictly "$coroutines_version"
        }
    }

    // Paging
    implementation 'androidx.paging:paging-runtime-ktx:3.2.1'

    // Room
    def room_version = "2.6.1"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    ksp "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-paging:$room_version"

    // ViewPager
    implementation 'androidx.viewpager2:viewpager2:1.0.0'

    // Glide
    def glide_version = "4.16.0"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    ksp "com.github.bumptech.glide:ksp:$glide_version"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glide_version"

    // Koin DI
    def koin_version = '3.5.6'
    implementation "io.insert-koin:koin-android:$koin_version"
    implementation "io.insert-koin:koin-androidx-navigation:$koin_version"

    // View binding delegate
    implementation 'com.github.kirich1409:viewbindingpropertydelegate:1.5.9'

    // jsonapi-converter
    implementation 'com.github.jasminb:jsonapi-converter:0.13'

    // Jackson
    def jackson_version = "2.17.0"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.11.0'
    implementation 'com.squareup.retrofit2:converter-jackson:2.11.0'

    // OkHttp
    def okhttp_version = "4.12.0"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    // Algolia Instantsearch
    def algolia_instantsearch_version = "3.1.3"
    implementation "com.algolia:instantsearch-android:$algolia_instantsearch_version"
    implementation "com.algolia:instantsearch-android-paging3:$algolia_instantsearch_version"
    implementation "com.algolia:instantsearch-coroutines-extensions:$algolia_instantsearch_version"

    // Kotlinx serialization
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.3'

    // Ktor client
    def ktor_version = "1.6.8"
    implementation "io.ktor:ktor-client-okhttp:$ktor_version"

    // Kotpref
    def kotpref_version = "2.13.2"
    implementation "com.chibatching.kotpref:kotpref:$kotpref_version"
    implementation "com.chibatching.kotpref:enum-support:$kotpref_version"
    implementation "com.chibatching.kotpref:livedata-support:$kotpref_version"

    // Security Crypto
    implementation 'androidx.security:security-crypto:1.1.0-alpha06'

    // TreeView
    implementation 'com.github.bmelnychuk:atv:1.2.9'

    // Expandable text view
    implementation 'at.blogc:expandabletextview:1.0.5'

    // CircleImageView
    implementation 'de.hdodenhof:circleimageview:3.1.0'

    // Material Rating Bar
    implementation 'me.zhanghai.android.materialratingbar:library:1.4.0'

    // MPAndroidCharts
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'

    // Photo View
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'

    // Hauler Gesture
    def hauler_version = "5.0.0"
    implementation "app.futured.hauler:hauler:$hauler_version"
    implementation "app.futured.hauler:databinding:$hauler_version"

    // AboutLibraries
    def aboutLibraries_version = "11.1.3"
    implementation "com.mikepenz:aboutlibraries-core:$aboutLibraries_version"
    implementation "com.mikepenz:aboutlibraries:$aboutLibraries_version"

    // LeakCanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.14'

    // Glide Transformations (only used for demo screenshots)
    if (screenshotMode) {
        debugImplementation 'jp.wasabeef:glide-transformations:4.3.0'
    }

    // Tests
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.assertj:assertj-core:3.25.3"
    testImplementation 'com.tngtech.archunit:archunit-junit4:1.3.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.5'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
    testImplementation "io.insert-koin:koin-test-junit4:$koin_version"
    testImplementation 'org.mockito.kotlin:mockito-kotlin:5.3.1'
    testImplementation 'net.datafaker:datafaker:2.0.1'

    // fastlane screengrab
    androidTestImplementation 'tools.fastlane:screengrab:2.1.1'
}